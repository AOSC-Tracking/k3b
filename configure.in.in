linux_scsi=no
AC_MSG_CHECKING(for linux scsi headers)
AC_TRY_COMPILE([
	#include <sys/types.h>
	#include <linux/../scsi/scsi.h> /* cope with silly includes */
	],[],[linux_scsi=yes
	])
AC_MSG_RESULT($linux_scsi)


AC_MSG_CHECKING(for cdparanoia libraries and headers)
AC_ARG_WITH(cdparanoia,
  [  --with-cdparanoia=DIR   Specify cdda libs dir (default=check)],
  [
    case "$withval" in
    yes)
      with_cdparanoia=CHECK
      ;;
    esac
  ],
  [
    with_cdparanoia=CHECK
  ]
)dnl

if test "x$with_cdparanoia" = "xCHECK" ; then
  with_cdparanoia=NOTFOUND
  search_incs="$kde_includes /usr/include /usr/local/include /usr/include/cdda /usr/local/include/cdda"
  AC_FIND_FILE(cdda_interface.h, $search_incs, para_incdir)
  if test -r $para_incdir/cdda_interface.h ; then
    test "x$para_incdir" != "x/usr/include" && CDPARANOIA_INCS="-I$para_incdir"
    with_cdparanoia=FOUND
  fi
  if test $with_cdparanoia = FOUND ; then
    with_cdparanoia=NOTFOUND
    # because of the horrible hack we need shared cdparanoia
    for ext in la so sl a ; do
      AC_FIND_FILE(libcdda_paranoia.$ext, $kde_libraries /usr/lib /usr/local/lib,
        para_libdir)
      if test -r $para_libdir/libcdda_paranoia.$ext ; then
        if test "x$para_libdir" != "x/usr/lib" ; then
          CDPARANOIA_LIBS="-L$para_libdir "
	  test "$USE_RPATH" = yes && CDPARANOIA_RPATH="-R $para_libdir"
        fi
        CDPARANOIA_LIBS="${CDPARANOIA_LIBS} -lcdda_paranoia -lcdda_interface"
	with_cdparanoia=FOUND
	# a.out shared libs don't have .so files, but .so.*
	if test $ext = a &&
	   ls $para_libdir | grep "^libcdda_paranoia.so.*" 2>&1 >/dev/null; then
          ext=so
        fi
        break
      fi
    done
    if test "$with_cdparanoia" = FOUND; then
      if test "$ext" = la; then
        grep "^library_names='.*[a-z].*'" $para_libdir/libcdda_paranoia.$ext 2>&1 > /dev/null || with_cdparanoia=NOTFOUND
      fi
      test "$ext" = a && with_cdparanoia=NOTFOUND
      test "$with_cdparanoia" = NOTFOUND && cdparanoia_only_static=yes
      # beware, that we don't reset CDPARANOIA_*
    fi
  fi
fi

case "$with_cdparanoia" in
no) AC_MSG_RESULT(no) ;;
NOTFOUND) AC_MSG_RESULT(searched but not found) ;;
*)
  if test "x$with_cdparanoia" = "xFOUND" ; then
    AC_MSG_RESULT(incs=$para_incdir libs=$para_libdir)
  else
    AC_MSG_RESULT($with_cdparanoia)
    CDPARANOIA_ROOT="$with_cdparanoia"
    if test "x$CDPARANOIA_ROOT" != "x/usr" ; then
      CDPARANOIA_INCS="-I${CDPARANOIA_ROOT}/include"
      CDPARANOIA_LIBS="-L${CDPARANOIA_ROOT}/lib "
      if test "$USE_RPATH" = "yes" ; then
        CDPARANOIA_RPATH="-R ${CDPARANOIA_ROOT}/lib"
      fi
    fi
    CDPARANOIA_LIBS="${CDPARANOIA_LIBS} -lcdda_paranoia -lcdda_interface"
  fi
  AC_DEFINE_UNQUOTED(HAVE_CDPARANOIA, 1, [Define if you have cdparanoia])
  AUDIOCD_SUBDIR="audiocd"
  ;;
esac

AC_SUBST(CDPARANOIA_INCS)
AC_SUBST(CDPARANOIA_LIBS)
#AC_SUBST(CDPARANOIA_RPATH)


OGG_VORBISLIBS=""
AC_MSG_CHECKING(for ogg/vorbis headers)
ogg_vorbis=no
AC_TRY_COMPILE([
		#include <vorbis/codec.h>
		#include <vorbis/vorbisfile.h>
                ],[
                ],[
                ogg_vorbis=yes
                ])
AC_MSG_RESULT($ogg_vorbis)
if test x$ogg_vorbis = xyes; then
   dnl we need the ogg_vorbis_lib because otherwise we override LIBS !
   AC_CHECK_LIB(vorbisfile,ov_open,ogg_vorbis_lib=yes,
                ogg_vorbis=no,[-lvorbisfile -lvorbis -logg])
fi
if test x$ogg_vorbis = xyes; then
   AC_DEFINE(OGG_VORBIS,1,[Define if you have ogg/vorbis installed])
   OGG_VORBISLIBS="-lvorbisfile -lvorbis -logg"
fi

AC_SUBST(OGG_VORBISLIBS)


if test x$ogg_vorbis = xno; then
	echo ""
	echo "No Ogg Vorbis library and headers found."
	echo "Ogg Vorbis support disabled."
	echo ""
fi



#the libmad stuff
dnl Compiler options.

arch=""
debug=""
optimize=""
profile=""

set -- $CFLAGS
CFLAGS="-Wall"

if test "$GCC" = yes
then
    case "$host" in
	*-pc-cygwin)
	    CPPFLAGS="$CPPFLAGS -mno-cygwin"
	    LDFLAGS="$LDFLAGS -mno-cygwin"
	    ;;
    esac
fi

while test $# -gt 0
do
    case "$1" in
	-Wall)
	    shift
	    ;;
	-g)
	    debug="-g"
	    shift
	    ;;
	-mno-cygwin)
	    shift
	    ;;
	-m*)
	    arch="$arch $1"
	    shift
	    ;;
	-O2)
	    optimize="-O"
	    shift
	    ;;
	-fomit-frame-pointer)
	    shift
	    ;;
	-O*|-f*)
	    optimize="$optimize $1"
	    shift
	    ;;
	*)
	    CFLAGS="$CFLAGS $1"
	    shift
	    ;;
    esac
done

if test "$GCC" = yes
then
    if test -z "$arch"
    then
	case "$host" in
	    i386-*)           ;;
	    i?86-*)           arch="-m486" ;;
	    arm*-empeg-*)     arch="-march=armv4 -mtune=strongarm1100" ;;
	    armv4*-*)         arch="-march=armv4 -mtune=strongarm" ;;
	    powerpc-*)        ;;
	    mips*-agenda-*)   arch="-mcpu=vr4100" ;;
	    mips*-luxsonor-*) arch="-mips1 -mcpu=r3000 -Wa,-m4010" ;;
	esac
    fi

    case "$optimize" in
	-O|"-O "*)
	    optimize="-O"
	    optimize="$optimize -fforce-mem"
	    optimize="$optimize -fforce-addr"
	    : #x optimize="$optimize -finline-functions"
	    : #- optimize="$optimize -fstrength-reduce"
	    optimize="$optimize -fthread-jumps"
	    optimize="$optimize -fcse-follow-jumps"
	    optimize="$optimize -fcse-skip-blocks"
	    : #x optimize="$optimize -frerun-cse-after-loop"
	    : #x optimize="$optimize -frerun-loop-opt"
	    : #x optimize="$optimize -fgcse"
	    optimize="$optimize -fexpensive-optimizations"
	    optimize="$optimize -fregmove"
	    : #* optimize="$optimize -fdelayed-branch"
	    : #x optimize="$optimize -fschedule-insns"
	    optimize="$optimize -fschedule-insns2"
	    : #? optimize="$optimize -ffunction-sections"
	    : #? optimize="$optimize -fcaller-saves"
	    : #> optimize="$optimize -funroll-loops"
	    : #> optimize="$optimize -funroll-all-loops"
	    : #x optimize="$optimize -fmove-all-movables"
	    : #x optimize="$optimize -freduce-all-givs"
	    : #? optimize="$optimize -fstrict-aliasing"
	    : #* optimize="$optimize -fstructure-noalias"

	    case "$host" in
		arm*-*)
		    # this may provoke bugs in gcc 2.95.2
		    optimize="$optimize -fstrength-reduce"
		    #
		    # this may actually be slower but it
		    # triggers bugs if not included
		    optimize="$optimize -finline-functions"
		    ;;
		mips*-*)
		    optimize="$optimize -fstrength-reduce"
		    optimize="$optimize -finline-functions"
		    ;;
		i?86-*)
		    optimize="$optimize -fstrength-reduce"
		    ;;
		*)
		    # this sometimes provokes bugs in gcc 2.95.2
		    : #optimize="$optimize -fstrength-reduce"
		    ;;
	    esac
	    ;;
    esac
fi

case "$host" in
    mips*-agenda-*)   AC_DEFINE(HAVE_MADD16_ASM,1,[test]) ;;
    mips*-luxsonor-*) AC_DEFINE(HAVE_MADD_ASM,1,[test])   ;;
esac

dnl Checks for header files.

AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(assert.h limits.h unistd.h sys/types.h fcntl.h errno.h)

dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T

AC_CHECK_SIZEOF(int, 2)
AC_CHECK_SIZEOF(long, 4)
AC_CHECK_SIZEOF(long long, 8)

dnl Checks for library functions.

AC_CHECK_FUNCS(waitpid fcntl pipe fork)

dnl Other options.

AC_SUBST(FPM)
AC_SUBST(ASO)
AC_SUBST(ASO_OBJS)

dnl handle --enable and --disable options

AC_CACHE_SAVE

AC_MSG_CHECKING(whether to optimize for speed or for accuracy)

AC_ARG_ENABLE(speed,
    [  --enable-speed          optimize for speed over accuracy], [
    case "$enableval" in
	yes)
	    optimize_for="speed"
	    AC_DEFINE(OPT_SPEED,1,[test])
	    ;;
    esac
])

AC_ARG_ENABLE(accuracy,
    [  --enable-accuracy       optimize for accuracy over speed], [
    case "$enableval" in
	yes)
	    if test "$optimize_for" = "speed"
	    then
		optimize_for="both"
	    else
		optimize_for="accuracy"
	    fi
	    AC_DEFINE(OPT_ACCURACY,1,[test])
	    ;;
    esac
])

AC_MSG_RESULT(${optimize_for-default})

if test "$optimize_for" = "both"
then
    AC_MSG_ERROR(cannot optimize for both speed and accuracy)
fi

AC_MSG_CHECKING(for architecture-specific fixed-point math routines)
AC_ARG_ENABLE(fpm,
    [  --enable-fpm=ARCH       use ARCH-specific fixed-point math routines
                          (one of: intel, arm, mips, sparc, ppc,
                           64bit, default)], [
    case "$enableval" in
	yes)                             ;;
	no|default|approx) FPM="DEFAULT" ;;
	intel|i?86)        FPM="INTEL"   ;;
	arm)               FPM="ARM"     ;;
	mips)              FPM="MIPS"    ;;
	sparc)             FPM="SPARC"   ;;
	ppc|powerpc)       FPM="PPC"     ;;
	64bit)             FPM="64BIT"   ;;
	float)             FPM="FLOAT"   ;;
	*)
	    AC_MSG_RESULT(failed)
	    AC_MSG_ERROR([bad --enable-fpm option])
	    ;;
    esac
])

if test -z "$FPM" && test "$GCC" = yes
then
    case "$host" in
	i?86-*)     FPM="INTEL"  ;;
	arm*-*)     FPM="ARM"    ;;
	mips*-*)    FPM="MIPS"   ;;
	sparc*-*)   FPM="SPARC"  ;;
	powerpc*-*) FPM="PPC"    ;;
	# FIXME: need to test for 64-bit long long...
    esac
fi

AC_MSG_RESULT(${FPM=DEFAULT})

if test "$FPM" = "DEFAULT"
then
    AC_MSG_WARN([default fixed-point math will yield limited accuracy])
fi

FPM="-DFPM_$FPM"

AC_ARG_ENABLE(sso,
    [  --enable-sso            use subband synthesis optimization], [
    case "$enableval" in
	yes) AC_DEFINE(OPT_SSO,1,[test]) ;;
    esac
])

AC_ARG_ENABLE(aso,
    [  --disable-aso           disable architecture-specific optimizations],
    [], [enable_aso=yes])

if test "$enable_aso" = yes
then
    case "$host" in
	i?86-*)
	    : #ASO="$ASO -DASO_INTERLEAVE1"
	    ASO="$ASO -DASO_ZEROCHECK"
	    : #not yet #ASO="$ASO -DASO_SYNTH"
	    : #not yet #ASO_OBJS="synth_mmx.lo"
	    ;;
	arm*-*)
	    ASO="$ASO -DASO_INTERLEAVE1"
	    ASO="$ASO -DASO_IMDCT"
	    ASO_OBJS="imdct_l_arm.lo"
	    ;;
	mips*-*)
	    ASO="$ASO -DASO_INTERLEAVE2"
	    ASO="$ASO -DASO_ZEROCHECK"
	    ;;
    esac
fi

AC_MSG_CHECKING(for ISO/IEC interpretation)
AC_ARG_ENABLE(strict-iso,
    [  --enable-strict-iso     use strict ISO/IEC interpretations], [
    case "$enableval" in
	yes)
	    AC_DEFINE(OPT_STRICT,1,[test])
	    interpretation="strict"
	    ;;
    esac
])
AC_MSG_RESULT(${interpretation-best accepted practices})

AC_MSG_CHECKING(whether to enable profiling)
AC_ARG_ENABLE(profiling,
    [  --enable-profiling      generate profiling code], [
    case "$enableval" in
	yes) profile="-pg" ;;
    esac
])
AC_MSG_RESULT(${enable_profiling-no})

AC_MSG_CHECKING(whether to enable debugging)
AC_ARG_ENABLE(debugging,
    [  --enable-debugging      enable diagnostic debugging support
  --disable-debugging     do not enable debugging and use more optimization], [
    case "$enableval" in
	yes)
	    AC_DEFINE(DEBUG,1,[test])
	    optimize=""
	    ;;
	no)
	    if test -n "$profile"
	    then
    AC_MSG_ERROR(--enable-profiling and --disable-debugging are incompatible)
	    fi

	    AC_DEFINE(NDEBUG,1,[test])
	    debug=""
	    if test "$GCC" = yes
	    then
		optimize="$optimize -fomit-frame-pointer"
	    fi
	    ;;
    esac
])
AC_MSG_RESULT(${enable_debugging-default})

AC_MSG_CHECKING(whether to enable experimental code)
AC_ARG_ENABLE(experimental,
    [  --enable-experimental   enable experimental code], [
    case "$enableval" in
	yes) AC_DEFINE(EXPERIMENTAL,1,[test]) ;;
    esac
])
AC_MSG_RESULT(${enable_experimental-no})

dnl Create output files.

test -n "$arch"     && CFLAGS="$CFLAGS $arch"
test -n "$debug"    && CFLAGS="$CFLAGS $debug"
test -n "$optimize" && CFLAGS="$CFLAGS $optimize"
test -n "$profile"  && CFLAGS="$CFLAGS $profile" LDFLAGS="$LDFLAGS $profile"

if test "x$linux_scsi" = "xno" -o "x$cdda_paranoia" = "xno" ; then
	DO_NOT_COMPILE="$DO_NOT_COMPILE k3b"
fi
